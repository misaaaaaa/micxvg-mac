#include <Arduino.h>
#include <LEDFader.h> // Include the LEDFader library for PWM control

// Pines de los LEDs
int relePins[] = {2, 3, 4, 5};

// para mapear comportamiento medido por sensor de temperatura
int comportamiento = 0;
int temperatureSensorPin = A1; // Pin analógico para el sensor de temperatura
int Temperature = 0;

// Definir los objetos LEDFader para los LEDs
LEDFader leds[] = { LEDFader(9), LEDFader(10) };
LEDFader &led1 = leds[0];
LEDFader &led2 = leds[1];


// Variables para almacenar la temperatura
int rawTemperature = 0;
unsigned long previousMillis = 0; // Store the last time the temperature was read
unsigned long currentMillis = 0;  // Store the current time
unsigned long interval = 1000;    // Interval at which to read the temperature

// Tiempos de encendido y apagado (ms) para cada RELE
int onTime1 = 510, offTime1 = 520;
int onTime2 = 530, offTime2 = 540;
int onTime3 = 550, offTime3 = 560;
int onTime4 = 570, offTime4 = 580;

// Estados y marcas de tiempo para cada LED
int releStates[4] = {LOW, LOW, LOW, LOW};
unsigned long prevMillis[4] = {0, 0, 0, 0};

void setup()
{
    Serial.begin(9600);
    // Declarar salidas
    for (int i = 0; i < 4; i++)
    {
        pinMode(relePins[i], OUTPUT);
    }
    // Primer fade para arrancar el ciclo
    led1.fade(255, 2000); // subir en 2s
    led2.fade(255, 1000); // subir en 1s
}

// funcion para animar los leds: siempre usa 0 como mínimo y 255 como máximo
void animarLed(LEDFader &led, int tSubida, int tBajada)
{
    // Si el LED no está haciendo fade, decide el próximo destino según su valor actual
    if (!led.is_fading())
    {
        if (led.get_value() == 0)
        {
            led.fade(255, tSubida); // subir de 0 a 255
        }
        else if (led.get_value() == 255)
        {
            led.fade(0, tBajada); // bajar de 255 a 0
        }
        // Si está en un valor intermedio, no hacer nada hasta que termine el fade
    }
}
// Función genérica para controlar un RELE usando arrays
void controlarRele(int pin, unsigned long onT, unsigned long offT,
                  int releIndex)
{

    if (releStates[releIndex] == HIGH && (currentMillis - prevMillis[releIndex] >= onT))
    {
        releStates[releIndex] = LOW;
        prevMillis[releIndex] = currentMillis;
        digitalWrite(pin, releStates[releIndex]);
    }
    else if (releStates[releIndex] == LOW && (currentMillis - prevMillis[releIndex] >= offT))
    {
        releStates[releIndex] = HIGH;
        prevMillis[releIndex] = currentMillis;
        digitalWrite(pin, releStates[releIndex]);
    }
}

void tReading()
{

    rawTemperature = analogRead(temperatureSensorPin); // Read the temperature sensor value

    // int Temperature = map(rawTemperature, 1023, 0, -40, 125); // Map the raw value to a temperature range
}

// funcion para decir que hace que cambie el comportamiento
void controlarComportamiento()
{
    if (currentMillis - previousMillis >= interval)
    {
        // comportamiento = (rawTemperature / 4) % 2;
        if (rawTemperature < 512)
        {
            comportamiento = 0; // Comportamiento 0 para temperaturas bajas
        }
        else
        {
            comportamiento = 1; // Comportamiento 1 para temperaturas altas
        }
        Serial.print("Raw Temperature: ");
        Serial.print(rawTemperature);
        Serial.print(" | Comportamiento: ");
        Serial.print(comportamiento);
        Serial.println();
        previousMillis = currentMillis; // Update previousMillis here
    }
}

void loop()
{
    currentMillis = millis();
    tReading();
    controlarComportamiento();

    led1.update();
    led2.update();

    // funcion para controlar encendidos y apagados de LEDS
    //  controlarRele(relePins[0], onTime1, offTime1, 0);
    //  controlarRele(relePins[1], onTime2, offTime2, 1);
    //  controlarRele(relePins[2], onTime3, offTime3, 2);
    //  controlarRele(relePins[3], onTime4, offTime4, 3);

    switch (comportamiento)
    {
    case 0:
        // Acción para comportamiento 0
        controlarRele(relePins[0], onTime1, offTime1, 0);
        controlarRele(relePins[1], onTime2, offTime2, 1);
        controlarRele(relePins[2], onTime3, offTime3, 2);
        controlarRele(relePins[3], onTime4, offTime4, 3);

        animarLed(led1, 2000, 2000);  
        animarLed(led2, 1000, 3000); 
        break;
    case 1:
        // Acción para comportamiento 1
        controlarRele(relePins[0], onTime1 * 2, offTime1 * 2, 0);
        controlarRele(relePins[1], onTime2 * 2, offTime2 * 2, 1);
        controlarRele(relePins[2], onTime3 * 2, offTime3 * 2, 2);
        controlarRele(relePins[3], onTime4 * 2, offTime4 * 2, 3);

        animarLed(led1, 400, 400);  
        animarLed(led2, 200, 600); 

        break;
    default:
        // Acción para otros valores (por si acaso)
        Serial.println("Comportamiento desconocido.");
        break;
    }

    // Serial.print("Comportamiento: ");
    // Serial.print(comportamiento);
    // Serial.print(" | sensorTemperatura: ");
    // Serial.print(rawTemperature);
    // Serial.println();
}
